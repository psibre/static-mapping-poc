plugins {
    id 'groovy'
}

repositories {
    jcenter()
}

dependencies {
    compile localGroovy()
    testCompile group: 'org.testng', name: 'testng', version: '6.14.3'
}

sourceSets {
    main {
        groovy {
            srcDirs = ["$buildDir/generatedSrc/main/groovy"]
        }
    }
    test {
        groovy {
            srcDirs = ["$buildDir/generatedSrc/test/groovy"]
        }
    }
}

tasks.register 'convertCsvToJson', ConvertCsvToJson, {
    srcFile = file('fooBar.csv')
    destFile = layout.buildDirectory.file('fooBar.json')
}

tasks.register 'generateSrc', GenerateSource, {
    srcFile = tasks.named('convertCsvToJson').get().destFile
    destSrcFile = file("$buildDir/generatedSrc/main/groovy/FooBar.groovy")
    destTestSrcFile = file("$buildDir/generatedSrc/test/groovy/FooBarTest.groovy")
}

tasks.withType(GroovyCompile) {
    dependsOn tasks.named('generateSrc')
}

test {
    useTestNG()
}
